#compdef dotfiles

_arguments -C \
  '1:cmd:->cmds' \
  '*:: :->args' \
&& return 0

local shared_args
shared_args=(
    "(- *)"{-h,--help}"[display usage information]"
)

case "$state" in
    (cmds)
        local commands=(
            "install:Install or update dotfiles"
            "settings:Use a settings file"
        )

        _arguments "(- *)"{-h,--help}"[display usage information]"

        _describe -t commands 'command' commands && return 0
        ;;
    (args)
        case $line[1] in
            (install)
                local arguments=(
                    "(-h --help --no-links)--no-copies[Only use dotfiles that are created as symlinks]"
                    "(-h --help --no-copies)--no-links[Only use dotfiles that are copied]"
                    "(-h --help)--no-backups[Don't backup any already existing files when copying or linking files]"
                    "$shared_args[@]"
                )

                _arguments "$arguments[@]" && return 0
            ;;
            (settings)
                local settings_file settings_files=()

                if [ -n "$DOTFILES_DIR" ] && [ -d "$DOTFILES_DIR" ]; then
                    for settings_file in "$DOTFILES_DIR"/settings/*; do
                        if [ -e "$settings_file" ] && [ ! -d "$settings_file" ]; then
                            settings_files=(
                                "$settings_files[@]"
                                "${settings_file#"${DOTFILES_DIR}/settings/"}"
                            )
                        fi
                    done
                fi

                _arguments "$shared_args[@]"
                _describe -t settings_files 'available settings' settings_files && return 0
                ;;
        esac
        ;;
esac

return 1
